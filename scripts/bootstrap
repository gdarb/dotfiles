#!/usr/bin/env fish

begin
    # account for running this in the `scripts` directory
    set -Ux DOTFILES (dirname (realpath (status dirname)))

    # grab utility functions
    source $DOTFILES/scripts/utilities

    # set XDG values if not already set
    if not set -q XDG_CONFIG_HOME
        set XDG_CONFIG_HOME $HOME/.config
    end

    if not set -q XDG_CACHE_HOME
        set XDG_CACHE_HOME $HOME/.cache
    end

    if not set -q XDG_DATA_HOME
        set XDG_DATA_HOME $HOME/.local/share
    end

    set BACKUP (mktemp -d -t dotfiles)

    for bootstrap in $DOTFILES/*/_bootstrap.fish
        set SRC (dirname $bootstrap)
        echo \n(set_color --bold)~~~ (basename $SRC) ~~~(set_color normal)
        source $bootstrap
    end
end
